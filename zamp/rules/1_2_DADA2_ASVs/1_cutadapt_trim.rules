## Remove primers from sequences
rule cutadapt_trim_paired:
    conda:
        os.path.join(dir.envs, "cutadapt.yml")
    container:
        singularity_envs["cutadapt"]
    input:
        R1_raw_reads=os.path.join(dir.out.fastq, "{sample}_R1.fastq.gz"),
        R2_raw_reads=os.path.join(dir.out.fastq, "{sample}_R2.fastq.gz"),
    output:
        R1_trimmed_reads=temp(
            os.path.join(
                dir.out.cutadapt,
                "{denoiser}",
                "1a_trimmed_primers",
                "{sample}_trimmed_R1.fastq.gz",
            )
        ),
        R2_trimmed_reads=temp(
            os.path.join(
                dir.out.cutadapt,
                "{denoiser}",
                "1a_trimmed_primers",
                "{sample}_trimmed_R2.fastq.gz",
            )
        ),
    log:
        os.path.join(
            dir.logs, "{denoiser}", "1a_trimmed_primers", "{sample}_trimmed_paired.txt"
        ),
    params:
        forward_primer=FW_PRIMER,
        reverse_primer=RV_PRIMER,
    threads: 1
    shell:
        """
        cutadapt \
        --cores {threads} \
        --error-rate 0.1 \
        --times 1 \
        --overlap 3 \
        -o {output.R1_trimmed_reads} \
        -p {output.R2_trimmed_reads} \
        -g '{params.forward_primer}' \
        -G '{params.reverse_primer}' \
        --match-read-wildcards \
        --discard-untrimmed \
        {input.R1_raw_reads} \
        {input.R2_raw_reads} > {log}
        """


rule cutadapt_trim_single:
    conda:
        os.path.join(dir.envs, "cutadapt.yml")
    container:
        singularity_envs["cutadapt"]
    input:
        R1_raw_reads=os.path.join(dir.out.fastq, "{sample}_single.fastq.gz"),
    output:
        R1_trimmed_reads=temp(
            os.path.join(
                dir.out.cutadapt,
                "{denoiser}",
                "1a_trimmed_primers",
                "{sample}_trimmed_single.fastq.gz",
            )
        ),
    log:
        os.path.join(
            dir.logs, "{denoiser}", "1a_trimmed_primers", "{sample}_trimmed_single.txt"
        ),
    params:
        forward_primer=FW_PRIMER,
        reverse_primer=RV_PRIMER,
        reverse_primer_compl=RV_PRIMER_COMPL,
    threads: 1
    shell:
        """
        cutadapt \
        --cores {threads} \
        --error-rate 0.1 \
        --times 1 \
        --overlap 3 \
        -o {output.R1_trimmed_reads} \
        -g '{params.forward_primer}' \
        -a '{params.reverse_primer_compl}' \
        --match-read-wildcards \
        --discard-untrimmed \
        {input[R1_raw_reads]} > {log}
        """

