## Get local reads.


rule copy_and_rename_paired_fastq:
    input:
        get_raw_reads,
    output:
        temp(os.path.join(dir.out.fastq, "{sample}_R1.fastq.gz")),
        temp(os.path.join(dir.out.fastq, "{sample}_R2.fastq.gz")),
    resources:
        max_copy=1,
    shell:
        """
        cp {input[0]} {output[0]} 
        cp {input[1]} {output[1]}
        """


rule copy_and_rename_single_fastq:
    input:
        get_raw_reads,
    output:
        temp(os.path.join(dir.out.fastq, "{sample}_single.fastq.gz")),
    resources:
        max_copy=1,
    shell:
        """
        cp {input[0]} {output[0]} 
        """


## Alternatively, obtain reads from SRA.
rule download_sra:
    conda:
        os.path.join(dir.envs, "sra-tools.yml")
    container:
        singularity_envs["sra-toolkit"]
    output:
        # /home/pipeline_user/ncbi/public
        temp("ncbi/public/sra/{sample}/{sample}.sra"),
    log:
        os.path.join(dir.logs, "samples/{sample}/sras/dump.txt"),
    params:
        sra_dir="$HOME/ncbi/public",
    resources:
        max_copy=1,
    shell:
        """
        #cache_dir=$(vdb-config --cfg -o n | grep "/repository/user/main/public/root" | cut -f2 -d'=' | sed "s/\\"//g")
        vdb-config --restore-defaults
        prefetch -v {wildcards.sample} -O "ncbi/public/sra/"  --max-size 100000000 &> {log}
        """


rule sra_convert_to_fastq_paired:
    conda:
        os.path.join(dir.envs, "sra-tools.yml")
    container:
        singularity_envs["sra-toolkit"]
    input:
        "ncbi/public/sra/{sample}/{sample}.sra",
    output:
        temp("{sample,[A-Za-z0-9]+}_1.fastq.gz"),
        temp("{sample,[A-Za-z0-9]+}_2.fastq.gz"),
    log:
        os.path.join(dir.logs, "sra_download/{sample}_dump.txt"),
    shell:
        """
        fastq-dump --split-3 --gzip --log-level 1 --disable-multithreading --minReadLen 0 --outdir $(dirname {output[0]}) {input[0]} &> {log}
        """


rule sra_convert_to_fastq_single:
    conda:
        os.path.join(dir.envs, "sra-tools.yml")
    container:
        singularity_envs["sra-toolkit"]
    input:
        "ncbi/public/sra/{sample}/{sample}.sra",
    output:
        temp("{sample,[A-Za-z0-9]+}.fastq.gz"),
    log:
        dir.logs + "/" + "logs/sra_download/{sample}_dump.txt",
    shell:
        """
        fastq-dump --gzip --outdir $(dirname {output[0]}) --log-level 0 --disable-multithreading --minReadLen 0 {wildcards.sample} &> {log}
        """
