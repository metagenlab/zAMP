## Set of rules to backup the input database and generate a hash code of these input files and the output files.


rule archive_original_files:
    input:
        fa=FASTA,
        tax=TAXONOMY,
    output:
        os.path.join("{prefix}", "database", "original_files.tar.gz"),
    shell:
        """
        tar czf {output} {input}
        """


rule clean_taxonomy:
    conda:
        os.path.join(dir.envs, "pandas.yml")
    container:
        singularity_envs["pandas"]
    input:
        TAXONOMY,
    output:
        temp(os.path.join("{prefix}", "clean_tax.tsv")),
    params:
        db_name=DBNAME,
        ranks=RANKS,
    script:
        os.path.join("scripts", "clean_tax.py")


rule hash_original_files_archive:
    input:
        os.path.join("{prefix}", "database", "original_files.tar.gz"),
    output:
        os.path.join("{prefix}", "hashes", "original.md5"),
    shell:
        """
        md5sum {input} | sort -k 2 | md5sum | cut -f 1 -d " " > {output}
        """


rule hash_amplicons_seq_tax:
    input:
        os.path.join("{prefix}", "database", "amplicons_tax.tsv"),
        os.path.join("{prefix}", "database", "amplicons_all_tax.tsv"),
        os.path.join("{prefix}", "database", "amplicons.fasta"),
    output:
        os.path.join("{prefix}", "hashes", "amplicons.md5"),
    shell:
        """
        md5sum {input} | sort -k 2 | md5sum | cut -f 1 -d " " > {output}
        """


## Generate a hash from all relevant files to insure that we always work with the same reference databases

hashes = {
    "rdp": os.path.join("{prefix}", "hashes", "rdp.md5"),
    "qiime2": os.path.join("{prefix}", "hashes", "qiime2.md5"),
    "dada2": os.path.join("{prefix}", "hashes", "dada2.md5"),
    "sintax": os.path.join("{prefix}", "hashes", "sintax.md5"),
    "kraken2": os.path.join("{prefix}", "hashes", "kraken2.md5"),
}

hash_out = [
    os.path.join("{prefix}", "hashes", "original.md5"),
    os.path.join("{prefix}", "hashes", "amplicons.md5"),
]
for classifier in CLASSIFIERS:
    hash_out.append(hashes[classifier])


rule hash_global_DB:
    input:
        hash_out,
    output:
        os.path.join("{prefix}", "database.md5"),
    shell:
        """
        md5sum {input} | sort -k 2 | md5sum | cut -f 1 -d " " > {output}
        """
