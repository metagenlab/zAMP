rule download_sra_single:
    conda:
        pipeline_path + "envs/sra-tools.yml"
    output:
        temp(link_directory + "{sample,[A-Za-z0-9]+}.fastq.gz"),
    log:
        logging_folder + "samples/{sample}/sras/dump.txt"
    shell:
        """
        vdb-config --restore-defaults
        fastq-dump --gzip --outdir $(dirname {output[0]}) --log-level 0 --disable-multithreading --minReadLen 0 {wildcards.sample} &> {log}
        cache_dir=$(vdb-config --cfg -o n | grep "/repository/user/main/public/root" | cut -f2 -d'=' | sed "s/\\"//g")
        if [ -f ${{cache_dir}}/sra/{wildcards.sample}.sra ]
        then
            rm ${{cache_dir}}/sra/{wildcards.sample}.sra
        fi
        if [ -f ${{cache_dir}}/sra/{wildcards.sample}.sra.cache ]
        then
            rm ${{cache_dir}}/sra/{wildcards.sample}.sra.cache
        fi
        """


rule download_sra_paired:
    conda:
        pipeline_path + "envs/sra-tools.yml"
    output:
        temp(link_directory + "{sample,[A-Za-z0-9]+}_1.fastq.gz"),
        temp(link_directory + "{sample,[A-Za-z0-9]+}_2.fastq.gz"),
    log:
        logging_folder + "samples/{sample}/sras/dump.txt"
    shell:
        """
        vdb-config --restore-defaults
        fastq-dump --split-3 --gzip --outdir $(dirname {output[0]}) --log-level 0 --disable-multithreading --minReadLen 0 {wildcards.sample} &> {log}
        cache_dir=$(vdb-config --cfg -o n | grep "/repository/user/main/public/root" | cut -f2 -d'=' | sed "s/\\"//g")
        if [ -f ${{cache_dir}}/sra/{wildcards.sample}.sra ]
        then
            rm ${{cache_dir}}/sra/{wildcards.sample}.sra
        fi
        if [ -f ${{cache_dir}}/sra/{wildcards.sample}.sra.cache ]
        then
            rm ${{cache_dir}}/sra/{wildcards.sample}.sra.cache
        fi
        """




